WHITESPACE = _{ " " | NEWLINE }

basic_term = _{ infimum | supremum | numeral | constant | variable }
    infimum = @{ "#infimum" | "#inf" }
    supremum = @{ "#supremum" | "#sup" }
    numeral = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
    constant = @{
        (!"not" | &("not" ~ ANY))
        ~ ("_" | "'")*
        ~ ASCII_ALPHA_LOWER
        ~ (ASCII_ALPHANUMERIC | "_" | "'")*
    }
    variable = @{
        ("_" | "'")*
        ~ ASCII_ALPHA_UPPER
        ~ (ASCII_ALPHANUMERIC | "_" | "'")*
    }

unary_operation = _{ negative }
    negative = { "-" }

binary_operation = _{ add | subtract | multiply | divide | modulo | interval }
    add = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide = { "/" }
    modulo = { "\\" }
    interval = { ".." }

term = { primary_term ~ (binary_operation ~ term)? | unary_operation ~ term }
    primary_term = _{ basic_term | "(" ~ term ~ ")"}

// TODO Tobias: Add rules for remaining syntax of ASP


test_infimum = _{ SOI ~ infimum ~ EOI }
test_supremum = _{ SOI ~ supremum ~ EOI }
test_numeral = _{ SOI ~ numeral ~ EOI }
test_constant = _{ SOI ~ constant ~ EOI }
test_variable = _{ SOI ~ variable ~ EOI }
test_term = _{ SOI ~ term ~ EOI }
