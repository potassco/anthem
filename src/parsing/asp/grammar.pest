WHITESPACE = _{ " " | NEWLINE }

constant = { infimum | integer | symbol | supremum }
    infimum = @{ "#infimum" | "#inf" }
    integer = @{ "0" | "-"? ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
    symbol = @{
        !negation
        ~ ("_" | "'")*
        ~ ASCII_ALPHA_LOWER
        ~ (ASCII_ALPHANUMERIC | "_" | "'")*
    }
    supremum = @{ "#supremum" | "#sup" }

variable = { named_variable | anonymous_variable }
    named_variable = @{
        ("_" | "'")*
        ~ ASCII_ALPHA_UPPER
        ~ (ASCII_ALPHANUMERIC | "_" | "'")*
    }
    anonymous_variable = @{ "_" }

unary_operator = _{ negative }
    negative = { !integer ~ "-" }

binary_operator = _{ add | subtract | multiply | divide | modulo | interval }
    add = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide = { "/" }
    modulo = { "\\" }
    interval = { ".." }

term = { unary_operator* ~ primary_term ~ (binary_operator ~ unary_operator* ~ primary_term)* }
    primary_term = _{ constant | variable | "(" ~ term ~ ")"}

atom = { symbol ~ tuple? }
    tuple = _{ "(" ~ (term ~ ("," ~ term)*)? ~ ")" }

sign = { negation{0, 2} }
    negation = @{ "not" ~ &(WHITESPACE | EOI)}

literal = { sign ~ atom }

relation = _{ equal | not_equal | less_equal | less | greater_equal | greater }
    equal = { "=" }
    not_equal = { "!=" }
    less = { "<" }
    less_equal = { "<=" }
    greater = { ">" }
    greater_equal = { ">=" }

comparison = { term ~ relation ~ term }

atomic_formula = { literal | comparison }

head = { basic_head | choice_head | constraint }
    basic_head = { atom }
    choice_head = { "{" ~ atom ~ "}" }
    constraint = { "#false"? }

body = { (atomic_formula ~ (("," | ";") ~ atomic_formula)*)? }

rule = { (!"." ~ head ~ (":-" ~ body)?) ~ "." }

program = { rule* }

// TODO Tobias: Add rules for remaining syntax of ASP
